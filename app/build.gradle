apply plugin: 'com.android.application'

def ENV_VARIABLE_PREFIX = "SIMRA_"
def API_ENDPOINT = "API_ENDPOINT"
def API_SECRET = "API_SECRET"
def SENTRY_DSN = "SENTRY_DSN"

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "dsp2020.simra.app"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 62
        versionName "61.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // defining the build date
        buildConfigField "long", "BUILD_DATE", System.currentTimeMillis() + "L"
        // define whether this build is a production build
        buildConfigField "boolean", "IS_PRODUCTION", "false"

        // Load `local.properties` (from root) for development configuration
        Properties properties = new Properties()
        if (project.rootProject.file('local.properties').exists()) {
            properties.load(project.rootProject.file('local.properties').newDataInputStream())
        }

        buildConfigField "String", SENTRY_DSN, properties.getProperty(SENTRY_DSN) ?: "\"\""
        def VAR_SENTRY_DSN = System.getenv(ENV_VARIABLE_PREFIX + SENTRY_DSN)
        if (VAR_SENTRY_DSN != null) {
            buildConfigField "String", SENTRY_DSN, "\"" + VAR_SENTRY_DSN + "\""
        }


        buildConfigField "String", API_ENDPOINT, properties.getProperty(API_ENDPOINT) ?: "\"https://localhost:8082/\""
        buildConfigField "String", API_SECRET, properties.getProperty(API_SECRET) ?: "\"mysecret\""

    }
    buildTypes {

        release {
            // Uncomment if you want to test the release configuration locally
            //signingConfig debug.signingConfig
            debuggable true
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // Allow loading the configuration Settings from environment variables
            buildConfigField "boolean", "IS_PRODUCTION", "true"
            def VAR_API_ENDPOINT = System.getenv(ENV_VARIABLE_PREFIX + API_ENDPOINT)
            if (VAR_API_ENDPOINT != null) {
                buildConfigField "String", API_ENDPOINT, "\"" + VAR_API_ENDPOINT + "\""
            }
            def VAR_API_SECRET = System.getenv(ENV_VARIABLE_PREFIX + API_SECRET)
            if (VAR_API_SECRET != null) {
                buildConfigField "String", API_SECRET, "\"" + VAR_API_SECRET + "\""
            }
        }
        debug {
            debuggable true
            applicationIdSuffix ".debug"
        }
        qa {
            initWith buildTypes.release
        }
    }
    compileOptions {
        sourceCompatibility = '1.8'
        targetCompatibility = '1.8'
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        htmlReport true
        htmlOutput file("lint-report.html")
        // Not yet enabled, too many errors. 
        //warningsAsErrors true
        abortOnError true
    }
    buildToolsVersion = '29.0.3'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'com.google.android.play:core:1.8.0'

    def lifecycle_version = "2.2.0"
    // optional - helpers for implementing LifecycleOwner in a Service
    implementation "androidx.lifecycle:lifecycle-service:$lifecycle_version"
    // implementation 'com.google.code.gson:gson:2.8.5'
    // implementation 'org.apache.commons:commons-csv:1.6'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.3.0-alpha01'
    api 'org.osmdroid:osmdroid-android:6.0.3'
    // api 'org.osmdroid:osmdroid-wms:6.0.3'
    // api 'org.osmdroid:osmdroid-mapsforge:6.0.3'
    // api 'org.osmdroid:osmdroid-geopackage:6.0.3'
    api 'com.github.MKergall:osmbonuspack:6.5.3'
    implementation 'com.github.Jay-Goo:RangeSeekBar:v2.0.4'
    implementation 'com.github.PhilJay:MPAndroidChart:v2.2.4'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.17'

    // Sentry Bug Reporting
    implementation 'io.sentry:sentry-android:2.1.6'

    // RingBuffer
    implementation 'org.apache.commons:commons-collections4:4.4'

    // Tests
    testImplementation 'junit:junit:4.13'
    testImplementation "org.assertj:assertj-core:3.16.1"
    // androidTestImplementation 'com.android.support.test:runner:1.0.2'
    // androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    // CameraX core library using the camera2 implementation
    def camerax_version = "1.0.0-beta04"
    // The following line is optional, as the core library is included indirectly by camera-camera2
    implementation "androidx.camera:camera-core:${camerax_version}"
    implementation "androidx.camera:camera-camera2:${camerax_version}"
    // If you want to additionally use the CameraX Lifecycle library
    implementation "androidx.camera:camera-lifecycle:${camerax_version}"

    // "Backporting" Java 8 streams for Android < 24
    compile 'net.sourceforge.streamsupport:android-retrostreams:1.7.2'
}
